description = 'Kotlin Test JUnit'

apply plugin: 'kotlin-platform-jvm'

JvmToolchain.configureJvmToolchain(project, JdkMajorVersion.JDK_1_6)

def includeJava9 = BuildPropertiesExtKt.getIncludeJava9(project.kotlinBuildProperties)

sourceSets {
    if (includeJava9) {
        java9
    }
}

dependencies {
    expectedBy project(':kotlin-test:kotlin-test-annotations-common')
    api project(':kotlin-test:kotlin-test-jvm')
    api('junit:junit:4.13.2')
}


jar {
    manifestAttributes(manifest, project, 'Test', true)
    if (includeJava9) {
        from sourceSets.java9.output
    }
}

configureSourcesJar()
configureJavadocJar()

compileKotlin {
    kotlinOptions.freeCompilerArgs = [
        "-Xallow-kotlin-package",
        "-Xsuppress-deprecated-jvm-target-warning",
    ]
    kotlinOptions.moduleName = project.name
}

compileTestKotlin {
    kotlinOptions.freeCompilerArgs = [
        "-Xallow-kotlin-package",
        "-Xsuppress-deprecated-jvm-target-warning",
    ]
}

configureFrontendIr(project)

if (includeJava9) {
    LibrariesCommon.configureJava9Compilation(project, 'kotlin.test.junit')
}
