/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import static androidx.build.dependencies.DependenciesKt.*
import androidx.build.LibraryGroups
import androidx.build.LibraryVersions
import androidx.build.Publish

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("kotlin-android")
}

dependencies {
    implementation(KOTLIN_STDLIB)
    testImplementation(JUNIT)
    testImplementation(ROBOLECTRIC)

    testImplementation("org.jetbrains.kotlin:kotlin-compiler:$COMPOSE_VERSION")

    testImplementation(KOTLIN_STDLIB)
    testImplementation(project(":compose:compose-runtime"))
    testImplementation(project(":ui-android-view-non-ir"))
    testImplementation(project(":compose:compose-compiler-hosted"))
}

android {
    defaultConfig {
        minSdkVersion 16
        javaCompileOptions {
            annotationProcessorOptions {
                // The kotlin-compiler contains an annotation processor that we don't need to invoke
                // The following ensure a warning isn't reported by the android plugin.
                includeCompileClasspath false
            }
        }
    }
    lintOptions {
        disable("SyntheticAccessor")
    }
    testOptions {
        unitTests.all {
            // There is only one version of the compose plugin built so the debug tests are
            // sufficient as they test that one version
            if (it.name == 'testReleaseUnitTest') {
                filter {
                    exclude '*'
                }
            }
        }
    }
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

androidx {
    name = "AndroidX Compiler CLI Tests"
    publish = Publish.NONE
    toolingProject = true
    mavenVersion = LibraryVersions.COMPOSE
    mavenGroup = LibraryGroups.COMPOSE
    inceptionYear = "2019"
    description = "Contains test for the compose compiler plugin"
}
