plugins {
    id 'kotlin-multiplatform'
}

// Determine host preset.
def hostPreset = MPPTools.defaultHostPreset(project)

kotlin {
    targetFromPreset(hostPreset, 'gitChurn') {
        binaries {
            executable() {
                entryPoint = 'sample.gitchurn.main'
                if (presets.mingwX64 == hostPreset) {
                    linkerOpts = ["-L${MPPTools.mingwPath()}/lib".toString()]
                    runTask.environment('PATH', "${MPPTools.mingwPath()}/bin".toString())
                }
                runTask.args(project.getRootProject().getRootDir().toString() + '/..')
            }
        }
        compilations.main.cinterops {
            libgit2 {
                switch (hostPreset) {
                    case presets.macosX64:
                        includeDirs.headerFilterOnly '/opt/local/include', '/usr/local/include'
                        break
                    case presets.linuxX64:
                        includeDirs.headerFilterOnly '/usr/include'
                        break
                    case presets.mingwX64:
                        includeDirs.headerFilterOnly "${MPPTools.mingwPath()}/include"
                        break
                }
            }
        }
    }
}
