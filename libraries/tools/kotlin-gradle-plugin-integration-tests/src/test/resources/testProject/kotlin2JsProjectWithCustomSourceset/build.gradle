buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin2js'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets {
    integrationTest
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

compileIntegrationTestKotlin2Js.dependsOn compileKotlin2Js

compileKotlin2Js.kotlinOptions.outputFile = "${buildDir}/kotlin2js/main/module.js"
compileIntegrationTestKotlin2Js.kotlinOptions.outputFile = "${buildDir}/kotlin2js/integrationTest/module-inttests.js"

def isIrBackend = project.findProperty("kotlin.js.useIrBackend")?.toBoolean() ?: false
if (isIrBackend) {
    compileKotlin2Js.kotlinOptions.freeCompilerArgs += ["-Xir-produce-klib-dir", "-Xir-only"]
    compileIntegrationTestKotlin2Js.kotlinOptions.freeCompilerArgs += ["-Xir-produce-js"]
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    integrationTestCompile files(file(compileKotlin2Js.kotlinOptions.outputFile).parent)
    integrationTestCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
}

task integrationTestJar(type: Jar) {
    from sourceSets.integrationTest.output
    classifier = 'inttests'
}

build.dependsOn integrationTestJar

task jarSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'source'
}
artifacts {
    compile jarSources
}
