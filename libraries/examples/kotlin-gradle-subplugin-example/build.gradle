import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import plugins.KotlinBuildPublishingPlugin

plugins {
    id("java-gradle-plugin")
    id("org.jetbrains.kotlin.jvm")
    id("com.gradle.plugin-publish")
    id("maven-publish")
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api project(':kotlin-gradle-plugin-api')
    // Use this dependency instead when building apart from the other modules:
    // compile "org.jetbrains.kotlin:kotlin-gradle-plugin-api:$kotlin_version"

    // kotlin-for-gradle-plugins-compilation
    String btaBootstrapVersion = libs.versions.kotlin.for.gradle.plugins.compilation.get()
    compileOnly("org.jetbrains.kotlin:kotlin-compiler-embeddable:$btaBootstrapVersion")
    compileOnly("org.jetbrains.kotlin:kotlin-stdlib:$btaBootstrapVersion")

    compileOnly gradleApi()
}

GradleCommonKt.configureCommonPublicationSettingsForGradle(project, false, false)
GradleCommonKt.configureKotlinCompileTasksGradleCompatibility(project)
GradleCommonKt.excludeGradleCommonDependencies(project, sourceSets.main)
TasksKt.optInToExperimentalCompilerApi(project)

extensions.extraProperties["kotlin.stdlib.default.dependency"] = "false"

// You should configure your own Gradle plugin publication!
//extensions.configure(GradlePluginDevelopmentExtension) {
//    it.setAutomatedPublishing(false)
//}

def sourceSet = sourceSets.main
def jarTask = tasks.named(sourceSet.jarTaskName, Jar) {
    archiveClassifier.set("unshaded")
}
def shadowJarTask = tasks.register(
        "${EmbeddableKt.EMBEDDABLE_COMPILER_TASK_NAME}${sourceSet.jarTaskName.capitalize()}",
        ShadowJar
) {
    def emptyClassifier = provider { "" }
    SetupPublicJar.setupPublicJar(
        it,
        jarTask.flatMap { it.archiveBaseName },
        jarTask.flatMap { emptyClassifier }
    )
    AddEmbeddedRuntime.addEmbeddedRuntime(it)
    it.from(sourceSet.output)
    EmbeddableKt.configureEmbeddableCompilerRelocation(it, true)
}

// Removing artifact produced by Jar task
configurations[sourceSet.runtimeElementsConfigurationName].artifacts.removeAll { true }
configurations[sourceSet.apiElementsConfigurationName].artifacts.removeAll { true }

// Adding instead artifact from shadow jar task
artifacts {
    add(sourceSet.runtimeElementsConfigurationName, shadowJarTask)
    add(sourceSet.apiElementsConfigurationName, shadowJarTask)
}

publishing {
    publications {
        it.withType(MavenPublication).configureEach {
            if (name.endsWith("PluginMarkerMaven")) {
                pom {
                    // https://github.com/gradle/gradle/issues/8754
                    // and https://github.com/gradle/gradle/issues/6155
                    packaging = "pom"
                }
            }
        }
    }
}


gradlePlugin {
    plugins {
        create("gradle-subplugin-example") {
            id = "org.jetbrains.kotlin.gradle-subplugin-example"
            implementationClass = "example.ExampleSubplugin"
            displayName = "Kotlin Gradle subplugin example"
        }
    }
}

// Disable releasing for this plugin
// It is not intended to be released publicly
tasks.withType(PublishToMavenRepository)
        .configureEach {
            if (it.name.endsWith("PublicationTo${KotlinBuildPublishingPlugin.REPOSITORY_NAME}Repository")) {
                setEnabled(false)
            }
        }

tasks.named("publishPlugins") {
    enabled = false
}
