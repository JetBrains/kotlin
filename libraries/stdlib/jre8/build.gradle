description = 'Kotlin Standard Library JRE 8 extension'

dependencies {
    compile project(':kotlin-stdlib')
    compile project(':kotlin-stdlib-jre7')
    testCompile project(':kotlin-test-parent:kotlin-test-junit')
}

sourceSets {
    main {
        kotlin {
            srcDir 'src'
        }
    }
    test {
        kotlin {
            srcDir 'test'
            srcDir '../test'
            srcDir '../jre7/test'
        }
    }
}

jar {
    manifest {
        attributes \
            'Kotlin-Runtime-Component': 'Core',
            'Kotlin-Version': "$kotlin_language_version",
            'Implementation-Title': "${project.description ?: project.name}"
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

compileKotlin {
    kotlinOptions.jdkHome = JDK_18
    kotlinOptions.jvmTarget = 1.8
    kotlinOptions.freeCompilerArgs = [
            "-Xallow-kotlin-package",
            "-Xmultifile-parts-inherit",
            "-Xdump-declarations-to", "${buildDir}/stdlib-jre8-declarations.json",
            "-module-name", project.name
    ]
}

compileTestKotlin {
    kotlinOptions.jdkHome = JDK_18
    kotlinOptions.jvmTarget = 1.8
    kotlinOptions.freeCompilerArgs = ["-Xallow-kotlin-package"]
}
