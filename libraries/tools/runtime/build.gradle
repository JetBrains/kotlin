description 'Kotlin Runtime (deprecated, use koltin-stdlib artifact instead)'

apply plugin: 'kotlin'

configureJvm6Project(project)
configurePublishing(project)

dependencies {
    compile group: 'org.jetbrains', name: 'annotations', version:'13.0'
}

sourceSets {
    main {
        if(!System.properties.'idea.active') {
            java {
                srcDir "${rootDir}/core/builtins/src"
                srcDir "${rootDir}/core/runtime.jvm/src"
                exclude 'org/jetbrains/annotations/**'
            }
            kotlin {
                exclude 'org/jetbrains/annotations/**'
            }
        }
    }

}

jar {
    manifestAttributes(manifest, project, 'Main')
    from("${rootDir}/dist/builtins")
}

sourcesJar {
    classifier = 'sources'
    from sourceSets.main.kotlin
    from "${rootDir}/core/builtins/native"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = [
                "-Xallow-kotlin-package",
                "-Xdump-declarations-to", "${buildDir}/runtime-declarations.json",
                "-cp", "${rootDir}/dist/builtins",
                "-module-name", project.name
        ]
    }
}
