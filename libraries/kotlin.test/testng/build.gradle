description = 'Kotlin Test TestNG'

apply plugin: 'kotlin-platform-jvm'

JvmToolchain.configureJvmToolchain(project, JdkMajorVersion.JDK_1_7)
JvmToolchain.updateJvmTarget(project, "1.6")

def includeJava9 = BuildPropertiesExtKt.getIncludeJava9(project.kotlinBuildProperties)

sourceSets {
    if (includeJava9) {
        java9
    }
}

dependencies {
    expectedBy project(':kotlin-test:kotlin-test-annotations-common')
    compile project(':kotlin-test:kotlin-test-jvm')
    compile('org.testng:testng:6.13.1')
    if (includeJava9) {
        java9CompileOnly('org.testng:testng:7.0.0')
    }
}

jar {
    manifestAttributes(manifest, project, 'Test', true)
    if (includeJava9) {
        from sourceSets.java9.output
    }
}

configureSourcesJar()
configureJavadocJar()

compileKotlin {
    kotlinOptions.freeCompilerArgs = [
        "-Xallow-kotlin-package",
        "-Xsuppress-deprecated-jvm-target-warning",
    ]
    kotlinOptions.moduleName = project.name
}

compileTestKotlin {
    kotlinOptions.freeCompilerArgs = [
        "-Xallow-kotlin-package",
        "-Xsuppress-deprecated-jvm-target-warning",
    ]
}

configureJvmIrBackend(project)

if (includeJava9) {
    LibrariesCommon.configureJava9Compilation(project, 'kotlin.test.testng')
}

test {
    useTestNG()
}
