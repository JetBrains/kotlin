buildscript {
    repositories {
       jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle-experimental:0.9.2'
    }

    repositories {
        mavenCentral()
        maven {
            url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:+"
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: "konan"

def platforms = ["armeabi-v7a", "arm64-v8a"]
konan.targets = ["android_arm32", "android_arm64"]

konanArtifacts {
    program('Polyhedron') {
        baseDir 'Polyhedron'
        artifactName 'libpoly'
    }
}

apply plugin: "com.android.model.application"

model {
    android {
        compileSdkVersion = 25
        buildToolsVersion = '25.0.2'

        defaultConfig {
            applicationId = 'com.android.konan_activity'
            minSdkVersion.apiLevel   9
            targetSdkVersion.apiLevel  25
        }

        ndk {
            moduleName = "polyhedron"
        }

        productFlavors {
            create("arm") {
                ndk {
                    abiFilters.addAll(platforms)
                }
            }
        }
    }

    repositories {
        libs(PrebuiltLibraries) {
            libpoly {
                    binaries.withType(SharedLibraryBinary) {
                        def name = targetPlatform.getName()
                        def index = platforms.indexOf(name)
                        if (index >= 0)
                            sharedLibraryFile = konanArtifacts.Polyhedron."${konan.targets[index]}".artifact
                    }
            }
        }
    }
    android.sources {
        main {
            jniLibs {
                dependencies {
                    library "libpoly" 
                }
            }
        }
    }
}

tasks.matching { it.name == 'preBuild' }.all {
    it.dependsOn 'compileKonan'
}

task buildApk(type: DefaultTask) {
    dependsOn "compileKonan"
    dependsOn "assembleDebug"
}