buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'java'
}

apply plugin: 'org.jetbrains.kotlin.platform.js'

def outDir = "${buildDir}/kotlin2js/main/"
compileKotlin2Js.kotlinOptions.moduleKind = "amd"
compileKotlin2Js.kotlinOptions.outputFile = outDir + "app.js"

compileKotlin2Js.kotlinOptions.freeCompilerArgs += ["-Xir-produce-js"]

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    implementation "org.mozilla:rhino:1.7.7.1"
}

tasks.register("runRhino", JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    workingDir = "${buildDir}/kotlin2js/main/"
    mainClass = 'org.mozilla.javascript.tools.shell.Main'
    args = ["-opt", "-1", "-f", "amd.js", "-f", "kotlin.js", "-f", "app.js", "-f", "check.js"]
}

build.doLast {
    configurations.compileClasspath.each { File file ->
        copy {
            includeEmptyDirs = false

            from zipTree(file.absolutePath)
            into "${buildDir}/kotlin2js/main/"
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
            }
        }
    }
    copy {
        from "."
        include "amd.js"
        include "check.js"
        into "${buildDir}/kotlin2js/main/"
    }
}
runRhino.dependsOn build