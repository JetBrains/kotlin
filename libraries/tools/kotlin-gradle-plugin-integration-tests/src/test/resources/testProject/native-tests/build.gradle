group 'com.example'
version '1.0'

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin-multiplatform'

repositories {
    mavenLocal()
    jcenter()
}

kotlin {
    targets {
        fromPreset(presets.macosX64, 'macos64')
        fromPreset(presets.linuxX64, 'linux64')
        fromPreset(presets.mingwX64, 'mingw64')

        fromPreset(presets.iosX64, 'ios')

        // Test creating and accessing test binaries for cross-targets
        fromPreset(presets.wasm32, 'wasm')

        configure([macos64, linux64, mingw64, wasm]) {
            compilations.create("anotherTest")
            binaries {
                test("another", [DEBUG]) {
                    compilation = compilations.anotherTest
                }
            }
        }
    }

    sourceSets {
        anotherTest
        macos64AnotherTest.dependsOn(anotherTest)
        linux64AnotherTest.dependsOn(anotherTest)
        mingw64AnotherTest.dependsOn(anotherTest)
    }
}

// Check that test events are correctly reported in CLI.
configure([macos64Test, linux64Test, mingw64Test, iosTest]){
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// Check that getting a test binary in an old way fails showing the corresponding warning
task checkOldGet {
    doLast {
        kotlin.targets {
            configure([macos64, linux64, mingw64]) {
                println("Get test: ${binaries.getExecutable("test", DEBUG)}")
            }
        }
    }
}

// Check that finding a test binary in an old way returns null showing the corresponding warning.
task checkOldFind {
    doLast {
        kotlin.targets {
            configure([macos64, linux64, mingw64]) {
                println("Find test: ${binaries.findExecutable("test", DEBUG)}")
            }
        }
    }
}

task checkNewGetters {
    doLast {
        kotlin.targets {
            configure([macos64, linux64, mingw64, wasm]) {
                println("Get test: ${binaries.getTest(DEBUG).outputFile.name}")
                println("Find test: ${binaries.findTest(DEBUG).outputFile.name}")
                println("Get test: ${binaries.getTest("another", DEBUG).outputFile.name}")
                println("Find test: ${binaries.findTest("another", DEBUG).outputFile.name}")
            }
        }
    }
}
