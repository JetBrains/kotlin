// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {        
        classpath 'com.android.tools.build:gradle:3.3.2'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}
apply plugin: 'com.android.application'

repositories {
    google()
    jcenter()
}

android {
    compileSdkVersion 19
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "org.jetbrains.kotlin.android.tests"
        minSdkVersion 19
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
        testApplicationId "org.jetbrains.kotlin.android.tests.gradle"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions { exclude 'META-INF/build.txt' }

    //TODO run under java 6, cause there is error on implicit 'stream' import in 'asWithMutable' test
    lintOptions {
        abortOnError false
    }

    compileOptions {
        incremental = false
    }

    dexOptions {
        dexInProcess false
        javaMaxHeapSize "1200m"
        maxProcessCount 4
        additionalParameters "--debug"
    }

    testOptions {
        resultsDir = "build/test/results"
    }


    flavorDimensions "box"

    productFlavors {
        ktest0 {
            dimension "box"
        }

        ktest1 {
            dimension "box"
        }
    }

}

task jarTestFolders() {
    println "Jar folders..."
    new File("${projectDir}/libs/").listFiles().each { File file ->
        if (file.isDirectory() && !file.name.equals("test")) {
            println "Jar '${file.name}' folder..."
            ant.jar(basedir: "libs/${file.name}/", destfile: "libs/test/" +  file.name  + ".jar")
        }
    }
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn jarTestFolders
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'

    ktest0Implementation fileTree(dir: 'libs/test', include: ['libtest0.jar'])
    ktest1Implementation fileTree(dir: 'libs/test', include: ['libtest1.jar'])
}
