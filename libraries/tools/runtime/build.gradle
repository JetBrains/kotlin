
description 'Kotlin Runtime'

dependencies {
    compile group: 'org.jetbrains', name: 'annotations', version:'13.0'
}

sourceSets {
    main {
        java {
            srcDir "${rootDir}/../core/builtins/src"
            srcDir "${rootDir}/../core/runtime.jvm/src"
            exclude 'org/jetbrains/annotations/**'
        }
        kotlin {
            exclude 'org/jetbrains/annotations/**'
        }
    }
}

jar {
    manifest {
        attributes 'Kotlin-Runtime-Component': 'Core',
                'Kotlin-Version': "$kotlin_language_version",
                'Implementation-Title': "${project.description ?: project.name}"
    }
    from("${rootDir}/../dist/builtins")
}

sourcesJar {
    classifier = 'sources'
    from sourceSets.main.kotlin
    from "${rootDir}/../core/builtins/native"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    options.fork = true
    // TODO: find how to fix on macos
//    options.bootClasspath = "${System.getenv('JDK_16')}/jre/lib/rt.jar"
}

compileKotlin {
    kotlinOptions.freeCompilerArgs = [
            "-Xallow-kotlin-package",
            "-Xdump-declarations-to", "${buildDir}/runtime-declarations.json",
            "-cp", "${rootDir}/../dist/builtins",
            "-module-name", project.name
    ]
}
