buildscript {
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:6.2.2'
    }
}

plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm"
}

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    resolvableImplementation {
        extendsFrom(findByName("implementation"))
        canBeResolved = true
    }
}

tasks.register("obfuscate", proguard.gradle.ProGuardTask) {
    dependsOn(jar)
    injars "${jar.archivePath}"
    outjars "$buildDir/proguard/out.jar"

    if (JavaVersion.current().isJava9Compatible()) {
        libraryjars "${System.getProperty('java.home')}/jmods"
    } else {
        libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    }

    libraryjars configurations.named("resolvableImplementation")

    dontobfuscate
    dontoptimize
    keep 'class *'
    keepclassmembers 'class * { \
            public <init>(...); \
            }'

}

configurations {
    proguarded
}

artifacts {
    proguarded(file(obfuscate.outJarFiles[0])) {
        name 'out'
        type 'jar'
        builtBy obfuscate
    }
}
