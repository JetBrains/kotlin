buildscript {
    repositories {
        mavenCentral()
        maven {
            url  "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
        }
    }

   dependencies {
     classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
   }
}

apply plugin: 'kotlin'
apply plugin: 'c'

def javaHome = System.getProperty('java.home')
def compilerArgsForJniIncludes = ["", "linux", "darwin"].collect { "-I$javaHome/../include/$it" } as String[]

model {
    components {
        callbacks(NativeLibrarySpec) {
            sources.c.source {
                srcDir 'src/callbacks/c'
                include '**/*.c'
            }
            binaries.all {
                cCompiler.args compilerArgsForJniIncludes
                cCompiler.args "-I$libffiDir/include"

                linker.args "$libffiDir/lib/libffi.a"
            }
        }
   }

}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

sourceSets.main.kotlin.srcDirs += "src/jvm/kotlin"


task nativelibs(type: Copy) {
    dependsOn 'callbacksSharedLibrary'

    from "$buildDir/libs/callbacks/shared/"
    into "$buildDir/nativelibs/"
}

classes.dependsOn nativelibs
