buildscript {
    repositories {
        jcenter()
        mavenCentral()
        google()
        maven {
            url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:${project.property('konan.plugin.version')}"
        classpath 'com.android.tools.build:gradle:3.1.2'
    }
}

repositories {
    jcenter()
    google()
}

apply plugin: 'konan'
apply plugin: 'com.android.application'

konan.targets = ['android_arm32', 'android_arm64']

def outDir = file('Polyhedron')
def libsDir = file("$outDir/libs")
def platforms = [
        "armeabi-v7a": [konanTarget: "android_arm32"],
        "arm64-v8a"  : [konanTarget: "android_arm64"]
]

konanArtifacts {
    program('Polyhedron') {
        artifactName 'libpoly'
    }
}

task copyLibs(type: Copy) {
    dependsOn konanArtifacts.Polyhedron
    destinationDir libsDir

    platforms.each { name, platform ->
        into(name) {
            from konanArtifacts.Polyhedron."${platform.konanTarget}".artifact
        }
    }
}

task deleteOut(type: Delete) {
    delete outDir
}

clean.dependsOn deleteOut

tasks.matching { it.name == 'preBuild' }.all {
    it.dependsOn copyLibs
}

android {
    compileSdkVersion 25

    defaultConfig {
        applicationId 'com.jetbrains.konan_activity2'
        minSdkVersion 9
        targetSdkVersion 25

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDir libsDir
        }
    }

}

task buildApk(type: Copy) {
    dependsOn "assembleDebug"
    destinationDir outDir
    from 'build/outputs/apk'
}
