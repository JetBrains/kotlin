description = 'Kotlin Test TestNG'

apply plugin: 'kotlin-platform-jvm'

JvmToolchain.configureJvmToolchain(project, JdkMajorVersion.JDK_1_8)

def includeJava9 = BuildPropertiesExtKt.getIncludeJava9(project.kotlinBuildProperties)

sourceSets {
    if (includeJava9) {
        java9
    }
}

dependencies {
    expectedBy project(':kotlin-test:kotlin-test-annotations-common')
    api project(':kotlin-test:kotlin-test-jvm')
    api('org.testng:testng:6.13.1')
    if (includeJava9) {
        java9CompileOnly('org.testng:testng:7.0.0')
    }
}

jar {
    LibrariesCommon.manifestAttributes(project, manifest, 'Test', true)
    if (includeJava9) {
        from sourceSets.java9.output
    }
}

RepoArtifacts.sourcesJar(project)
RepoArtifacts.javadocJar(project)

compileKotlin {
    kotlinOptions.freeCompilerArgs = [
        "-Xallow-kotlin-package",
    ]
    kotlinOptions.moduleName = project.name
}

compileTestKotlin {
    kotlinOptions.freeCompilerArgs = [
        "-Xallow-kotlin-package",
    ]
}

LibrariesCommon.configureFrontendIr(project)

if (includeJava9) {
    LibrariesCommon.configureJava9Compilation(project, 'kotlin.test.testng')
}

test {
    useTestNG()
}
