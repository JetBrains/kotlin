apply plugin: 'kotlin'

sourceSets {
    main {
        kotlin.srcDir 'src'
        resources.srcDir "$buildDir/copyright"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$bootstrapKotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$bootstrapKotlinVersion"
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-version"]
    }
}

task copyCopyrightProfile(type: Copy) {
    from "$rootDir/.idea/copyright"
    into "$buildDir/copyright"
    include 'apache.xml'
}

processResources {
    dependsOn(copyCopyrightProfile)
}

task run(type: JavaExec) {
    group 'application'
    main 'generators.GenerateStandardLibKt'
    classpath sourceSets.main.runtimeClasspath
    args = ["${rootDir}"]
    systemProperty 'line.separator', '\n'
}

task generateUnicodeData(type: JavaExec) {
    group 'application'
    main 'generators.unicode.GenerateUnicodeDataKt'
    classpath sourceSets.main.runtimeClasspath
    args = ["${rootDir}"]
}