
description 'Kotlin Runtime'

dependencies {
    compile group: 'org.jetbrains', name: 'annotations', version:'13.0'
}

sourceSets {
    if(!System.properties.'idea.active')
    main {
        java {
            srcDir "${rootDir}/../core/builtins/src"
            srcDir "${rootDir}/../core/runtime.jvm/src"
            exclude 'org/jetbrains/annotations/**'
        }
        kotlin {
            exclude 'org/jetbrains/annotations/**'
        }
    }
}

jar {
    manifestAttributes(manifest, project, 'Core')
    from("${rootDir}/../dist/builtins")
}

sourcesJar {
    classifier = 'sources'
    from sourceSets.main.kotlin
    from "${rootDir}/../core/builtins/native"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    options.fork = true
    options.forkOptions.executable = "${JDK_16}/bin/javac"
}

compileKotlin {
    kotlinOptions {
        jdkHome = JDK_16
        freeCompilerArgs = [
                "-Xallow-kotlin-package",
                "-Xdump-declarations-to", "${buildDir}/runtime-declarations.json",
                "-cp", "${rootDir}/../dist/builtins",
                "-module-name", project.name
        ]
    }
}
