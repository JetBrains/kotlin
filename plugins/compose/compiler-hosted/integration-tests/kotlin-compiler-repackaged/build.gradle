/*
 * Copyright 2021 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */
import androidx.build.Publish
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id("java")
    id("AndroidXPlugin")
}

dependencies {
    compileOnly(libs.kotlinCompiler)
}

// This task exists to work around https://youtrack.jetbrains.com/issue/KT-44876
TaskProvider<ShadowJar> shadowJar = tasks.register("repackageCompiler", ShadowJar) { task ->
    task.configurations = [project.configurations.compileClasspath]
    task.relocate("kotlinx.collections.immutable", "org.jetbrains.kotlin.kotlinx.collections.immutable")
    task.destinationDirectory.set(new File(buildDir, "repackaged"))
    task.archiveVersion.set("")
}

configurations {
    apiElements.outgoing.artifacts.clear()
    runtimeElements.outgoing.artifacts.clear()
}

artifacts {
    apiElements(shadowJar)
    runtimeElements(shadowJar)
}

androidx {
    // This module exists to work around https://youtrack.jetbrains.com/issue/KT-44876
    name = "Repackaged version of Kotlin compiler"
    publish = Publish.NONE
    inceptionYear = "2021"
    description = "Contains the Kotlin compiler repackaged to be compatible with integration tests"
}
