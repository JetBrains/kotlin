description 'Kotlin Runtime (deprecated, use kotlin-stdlib artifact instead)'

apply plugin: 'kotlin'

configureJvm6Project(project)
configureDist(project)
configurePublishing(project)

dependencies {
    compile group: 'org.jetbrains', name: 'annotations', version:'13.0'
}

sourceSets {
    main {
        if(!System.properties.'idea.active') {
            java {
                srcDir "${rootDir}/core/builtins/src"
                srcDir "${rootDir}/libraries/stdlib/jvm/runtime"
            }
        }
    }

}

jar {
    manifestAttributes(manifest, project, 'Main')
    from("${rootDir}/dist/builtins")
}

sourcesJar {
    classifier = 'sources'
    from sourceSets.main.kotlin
    from "${rootDir}/core/builtins/native"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = [
                "-Xallow-kotlin-package",
                "-Xnormalize-constructor-calls=enable",
                "-Xdump-declarations-to=${buildDir}/runtime-declarations.json",
                "-cp", "${rootDir}/dist/builtins",
                "-module-name", project.name
        ]
    }
}
