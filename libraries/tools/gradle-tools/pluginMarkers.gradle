ext.pluginMarkerProject = { String pluginProjectId ->
    evaluationDependsOn(":$pluginProjectId")

    def pluginProject = findProject(":$pluginProjectId")

    repositories.addAll(pluginProject.repositories.toList())

    apply plugin: 'java'

    dependencies {
        compile pluginProject
    }

    configurations.archives.artifacts.clear()

    configurePublishing(project)

    task emptyJar(type: Jar)

    version = project.version + (findProperty('marker_version_suffix') ?: "")

    for (pluginId in pluginProject.pluginBundle.plugins*.id) {
        def artifactName = "${pluginId}.gradle.plugin"

        artifacts {
            ['', 'javadoc', 'sources'].forEach { theClassifier ->
                archives(emptyJar) {
                    group = pluginId
                    name = artifactName
                    classifier theClassifier
                }
            }
        }

        def setUpSeparateArtifact = {
            addFilter(pluginId) { artifact, file -> artifact.name == artifactName }
            def pom = pom(pluginId)
            pom.groupId = pluginId
            pom.artifactId = "${pluginId}.gradle.plugin"
        }

        configure(uploadArchives.repositories.mavenDeployer, setUpSeparateArtifact)
        configure(install.repositories.mavenInstaller, setUpSeparateArtifact)
    }

    publish.dependsOn uploadArchives
}