description = 'Kotlin Standard Library JRE 8 extension'

apply plugin: 'kotlin'

configureJvm6Project(project)
configurePublishing(project)
ext.javaHome = JDK_18

dependencies {
    compile project(':kotlin-stdlib')
    compile project(':kotlin-stdlib-jre7')
    testCompile project(':kotlin-test:kotlin-test-junit')
}

sourceSets {
    main {
        kotlin {
            srcDir 'src'
        }
    }
    test {
        kotlin {
            srcDir 'test'
            if(!System.properties.'idea.active') {
                srcDir '../test'
                srcDir '../jre7/test'
            }
        }
    }
}

jar {
    manifestAttributes(manifest, project, 'Main')
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dist {
    from (jar, sourcesJar)
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions.jdkHome = JDK_18
    kotlinOptions.jvmTarget = 1.8
}

compileKotlin {
    kotlinOptions.freeCompilerArgs = [
            "-Xallow-kotlin-package",
            "-Xmultifile-parts-inherit",
            "-Xdump-declarations-to", "${buildDir}/stdlib-jre8-declarations.json",
            "-module-name", project.name
    ]
}

compileTestKotlin {
    kotlinOptions.freeCompilerArgs = ["-Xallow-kotlin-package"]
}

kotlin.experimental.coroutines 'enable'

test {
    executable = "$JDK_18/bin/java"
}


task testJre6Tests(type: Test) {
    dependsOn project(':kotlin-stdlib').tasks.testClasses
    group = "verification"

    executable = "$JDK_18/bin/java"

    testClassesDir = project.file('../build/classes/test')
    classpath = project.files('../build/classes/test') + sourceSets.test.compileClasspath
}

check.dependsOn testJre6Tests