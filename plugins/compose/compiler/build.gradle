/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.LibraryType
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id("java")
    id("AndroidXPlugin")
}

dependencies {
    compileOnly(project(":compose:compiler:compiler-hosted"))
}

TaskProvider<ShadowJar> shadowJar = tasks.register("embeddedPlugin", ShadowJar) {
    configurations = [project.configurations.compileClasspath]
    relocate("com.intellij", "org.jetbrains.kotlin.com.intellij")
    archiveBaseName.set("embedded")
    archiveVersion.set("")
    destinationDirectory.set(new File(buildDir, "repackaged"))
}

configurations {
    // replace the standard jar with the one built by 'shadowJar' in both api and runtime variants
    apiElements.outgoing.artifacts.clear()
    apiElements.outgoing.artifact(shadowJar.flatMap {it.archiveFile})
    runtimeElements.outgoing.artifacts.clear()
    runtimeElements.outgoing.artifact(shadowJar.flatMap {it.archiveFile})
}

androidx {
    name = "Compose Compiler"
    type = LibraryType.COMPILER_PLUGIN
    mavenGroup = LibraryGroups.COMPOSE_COMPILER
    inceptionYear = "2019"
    description = "Compiler plugin that enables Compose"
}
