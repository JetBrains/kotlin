apply plugin: 'konan'

konan.targets = ['macbook', 'linux', 'raspberrypi']

konanArtifacts {

    interop ('sdl') {
        defFile 'src/main/c_interop/sdl.def'
        packageName 'sdl'

        target 'macbook', {
            includeDirs '/Library/Frameworks/SDL2.framework/Headers',
                    "${System.getProperty("user.home")}/Library/Frameworks/SDL2.framework/Headers",
                    '/opt/local/include/SDL2',
                    '/usr/local/include/SDL2'
        }

        target 'linux', {
            includeDirs '/usr/include/SDL2'
        }

        target 'raspberrypi', {
            includeDirs "${System.getProperty("user.home")}/.konan/dependencies/target-sysroot-1-raspberrypi/usr/include/SDL2"
        }
    }

    program ('tetris') {
        libraries {
            artifact 'sdl'
        }

        target 'macbook', {
            linkerOpts "-F ${System.getProperty("user.home")}/Library/Frameworks -F /Library/Frameworks -framework SDL2"
            // Use this line instead of the previous one if you've got a 'No SDL-framework' error.
            //linkerOpts "-L/opt/local/lib -L/usr/local/lib -lSDL2"
        }

        target 'linux', {
            linkerOpts '-L/usr/lib/x86_64-linux-gnu -lSDL2'
        }

        target 'raspberrypi', {
            linkerOpts '-lSDL2'
        }
    }
}

compileKonan {
    doLast {
        konanArtifacts.tetris.forEach() { task ->
            copy {
                from 'src/main/resources'
                into task.artifact.parentFile
            }
        }
    }
}