apply plugin: 'kotlin'

configurations {
    testArtifacts
}

dependencies {
    compile project(':kotlin-stdlib')
    compile project(':kotlinx-metadata')
    compile project(':kotlinx-metadata-jvm')
    compile 'org.ow2.asm:asm:6.0'
    compile 'org.ow2.asm:asm-tree:6.0'
    testCompile project(':kotlin-test:kotlin-test-junit')

    testArtifacts project(':kotlin-stdlib')
    testArtifacts project(':kotlin-stdlib-jre7')
    testArtifacts project(':kotlin-stdlib-jre8')
    testArtifacts project(':kotlin-stdlib-jdk7')
    testArtifacts project(':kotlin-stdlib-jdk8')
    testArtifacts project(':kotlin-reflect')
    // legacy
    testArtifacts project(':kotlin-runtime')
}

sourceSets {
    test {
        java {
            srcDir "src/test/kotlin"
        }
    }
}

def testCasesDeclarationsDump = "${buildDir}/cases-declarations.json".toString()

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xdump-declarations-to=$testCasesDeclarationsDump", "-Xnormalize-constructor-calls=enable"]
    }
}

test {
    dependsOn cleanCompileTestKotlin
    dependsOn configurations.testArtifacts

    systemProperty 'overwrite.output', System.getProperty("overwrite.output", "false")
    systemProperty 'kotlinVersion', project.version
    systemProperty 'testCasesClassesDirs', sourceSets.test.output.classesDirs.asPath
    systemProperty 'testCasesDeclarations', testCasesDeclarationsDump
    jvmArgs '-ea'

    ignoreFailures = System.getenv("kotlin_build_ignore_test_failures") == 'yes'
}
