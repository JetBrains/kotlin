apply plugin: 'base'

// kotlin/libraries/tools/kotlin-dokka-stdlib  ->  kotlin
final File kotlinRootDir = rootProject.file( "../../../")
final String kotlinVersion = "1.5.255-SNAPSHOT"

repositories {
  mavenLocal()
  mavenCentral()
}

final List<String> modules = [
        "kotlin-stdlib",
        "kotlin-stdlib-common",
        "kotlin-stdlib-jdk7",
        "kotlin-stdlib-jdk8",
        "kotlin-stdlib-js",
        "kotlin-test",
        "kotlin-test-js",
        "kotlin-test-junit5",
        "kotlin-test-junit",
        "kotlin-test-testng",
        "kotlin-test-common",
]


task extractLibs() { }


final File kotlinLibsDir = new File(buildDir, 'libs')

modules.forEach { module ->
  final String lib = "kotlin_lib_$module"

  final Configuration  lib_src = configurations.create(lib)

  if (module == "kotlin-test-js") {
    lib_src.attributes {attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, "kotlin-runtime")) }
  }

  dependencies {
    "$lib"(group: 'org.jetbrains.kotlin', name: module, version: kotlinVersion)
  }

  final Task libsTask = tasks.create("extract_lib_$module", Sync) {
    dependsOn lib_src

    from { lib_src }
    into "$kotlinLibsDir/$module"
  }

  extractLibs.dependsOn libsTask
}

project.extensions.kotlin_root = kotlinRootDir
project.extensions.kotlin_libs = kotlinLibsDir
