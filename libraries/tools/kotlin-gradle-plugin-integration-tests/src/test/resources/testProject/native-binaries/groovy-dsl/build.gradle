buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin-multiplatform'

repositories {
    mavenLocal()
    mavenCentral()
}

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                api(project(":exported"))
            }
        }
        
        commonTest {
            dependencies {
                api "org.jetbrains.kotlin:kotlin-test"
            }
        }
    }

    targets {
        macosX64("macos64")
        macosArm64("macosArm64")
        linuxX64("linux64")
        mingwX64("mingw64")

        configure([findByName('macos64'), findByName('macosArm64'), findByName('linux64'), findByName('mingw64')]) {
            compilerOptions.verbose.set(true)
            compilations.test.compileTaskProvider.configure {
                compilerOptions.freeCompilerArgs.add("-nowarn")
            }
            binaries {

                executable()                  // Executable with default name.
                executable("foo")             // Custom binary name.
                executable("bar", [RELEASE])  // Custom build types.

                // Configure a binary.
                executable("baz", [RELEASE]) {
                    // Rename an output binary: baz.kexe -> my-baz.kexe.
                    baseName = "my-baz"
                    // Use a custom entry point.
                    entryPoint = "foo.main"
                }

                executable("test2", [RELEASE]) {
                    compilation = compilations["test"]
                    freeCompilerArgs += "-tr"
                    linkTaskProvider.configure {
                        toolOptions {
                            freeCompilerArgs.add("-Xtime")
                        }
                    }
                }

                sharedLib([RELEASE]) {
                    export project(':exported')
                }
                staticLib([RELEASE]) {
                    export project(':exported')
                }
            }
            // Check that we can access binaries/tasks:
            // Just by name:
            println("Check link task: ${binaries.releaseShared.linkTaskProvider.name}")
            // Using a typed getter:
            println("Check run task: ${binaries.getExecutable("foo", RELEASE).runTask.name}")
        }
    }
}
