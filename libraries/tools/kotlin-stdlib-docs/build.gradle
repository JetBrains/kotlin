import org.jetbrains.dokka.Platform
import org.jetbrains.dokka.DokkaConfiguration
import org.jetbrains.dokka.gradle.DokkaTask

plugins {
    id "base"
    id "org.jetbrains.dokka"
}

evaluationDependsOnChildren()

def pKotlinBig() { return project('kotlin_big').extensions }
def pKotlinNative() { return project('kotlin_native').extensions }

ext.outputDir = "$buildDir/doc"
ext.kotlin_root = pKotlinBig().kotlin_root
ext.kotlin_libs = pKotlinBig().kotlin_libs
ext.kotlin_native_root = pKotlinNative().kotlin_native_root
ext.github_revision = pKotlinBig().github_revision
ext.localRoot = kotlin_root
ext.baseUrl = new URL("https://github.com/JetBrains/kotlin/tree/$github_revision")
ext.templatesDir = "$projectDir/templates".replace('\\', '/')

task cleanDocs(type: Delete) {
    delete(outputDir)
}

task prepare() {
    dependsOn(':kotlin_big:extractLibs')
}

repositories {
    mavenCentral()
    maven {
        url 'https://maven.pkg.jetbrains.space/kotlin/p/dokka/dev'
    }
}
final String dokka_version = findProperty("dokka_version")

dependencies {
    dokkaPlugin project(":plugins:dokka-samples-transformer-plugin")
    dokkaPlugin project(":plugins:dokka-stdlib-configuration-plugin")
    dokkaPlugin project(":plugins:dokka-version-filter-plugin")
    dokkaPlugin "org.jetbrains.dokka:versioning-plugin:$dokka_version"
}

TaskProvider<DokkaTask> createStdLibVersionedDocTask(String version, Boolean isLatest) {
    return tasks.register("kotlin-stdlib_" + version + (isLatest ? "_latest" : ""), DokkaTask) {
        dependsOn(prepare)

        def kotlin_libs = pKotlinBig().kotlin_libs
        def kotlin_native_root = pKotlinNative().kotlin_native_root
        def kotlin_native_libs = pKotlinNative().kotlin_native_libs

        def kotlin_stdlib_dir = "$kotlin_root/libraries/stdlib"

        def stdlibIncludeMd = "$kotlin_root/libraries/stdlib/src/Module.md"
        def stdlibSamples = "$kotlin_root/libraries/stdlib/samples/test"

        def stdlibCommonClasspath = ["$kotlin_libs/kotlin-stdlib-common/".toString()]
        def stdlibJvmClasspath = ["$kotlin_libs/kotlin-stdlib/".toString(), "$kotlin_libs/kotlin-stdlib-jdk7".toString(), "$kotlin_libs/kotlin-stdlib-jdk8/".toString(), "$kotlin_root/core/reflection.jvm/src".toString()]
        def stdlibNativeClasspath = ["$kotlin_native_libs/klib/common/stdlib".toString()]
        def stdlibJsClasspath = ["$kotlin_libs/kotlin-stdlib-js/".toString()]
        def suppressedPackages = [
                "kotlin.internal",
                "kotlin.jvm.internal",
                "kotlin.js.internal",
                "kotlin.native.internal",
                "kotlin.jvm.functions",
                "kotlin.coroutines.jvm.internal",
                "kotlin.reflect.jvm.internal"
        ]

        def kotlinLanguageVersion = version
        if (version == "1.0")
            kotlinLanguageVersion =  "1.1"


        moduleName.set("stdlib")
        if (isLatest) {
            outputDirectory.set(new File(outputDir, "/kotlin-stdlib/latest"))
            pluginsMapConfiguration.set(["org.jetbrains.dokka.base.DokkaBase"             : """{ "mergeImplicitExpectActualDeclarations": "true", "templatesDir": "$templatesDir" }""",
                                         "org.jetbrains.dokka.versioning.VersioningPlugin": """{ "version": "$version", "olderVersionsDir": "${outputDir.toString().replace('\\', '/')}//kotlin-stdlib/old" }"""])
        } else {
            outputDirectory.set(new File(outputDir, "/kotlin-stdlib/old/" + version))
            pluginsMapConfiguration.set(["org.jetbrains.dokka.base.DokkaBase"                : """{ "mergeImplicitExpectActualDeclarations": "true", "templatesDir": "$templatesDir" }""",
                                         "org.jetbrains.dokka.kotlinlang.VersionFilterPlugin": """{ "targetVersion": "$version" }""",
                                         "org.jetbrains.dokka.versioning.VersioningPlugin"   : """{ "version": "$version" }"""])
        }
        dokkaSourceSets {
            if (version != "1.0" && version != "1.1") { // Common platform since Kotlin 1.2
                register("kotlin-stdlib-common") {
                    jdkVersion.set(8)
                    platform.set(Platform.common)
                    noJdkLink.set(true)
                    classpath.setFrom(stdlibCommonClasspath)

                    displayName.set("Common")
                    sourceRoots.from("$kotlin_root/core/builtins/native")
                    sourceRoots.from("$kotlin_root/core/builtins/src/")

                    sourceRoots.from("$kotlin_stdlib_dir/common/src")
                    sourceRoots.from("$kotlin_stdlib_dir/src")
                    sourceRoots.from("$kotlin_stdlib_dir/unsigned/src")
                }
            }

            register("kotlin-stdlib-java-common") {
                jdkVersion.set(8)
                platform.set(Platform.jvm)
                classpath.setFrom(stdlibJvmClasspath)

                displayName.set("JVM")
                if (version != "1.0" && version != "1.1") {
                    dependsOn("kotlin-stdlib-common")
                }
                //sourceRoots.from("$kotlin_root/core/builtins/native")
                //sourceRoots.from("$kotlin_root/core/builtins/src")

                sourceRoots.from("$kotlin_stdlib_dir/jvm/src")

                sourceRoots.from("$kotlin_root/core/reflection.jvm/src")
                sourceRoots.from("$kotlin_stdlib_dir/jvm/runtime/kotlin/jvm/annotations")
                sourceRoots.from("$kotlin_stdlib_dir/jvm/runtime/kotlin/jvm/JvmClassMapping.kt")
                sourceRoots.from("$kotlin_stdlib_dir/jvm/runtime/kotlin/jvm/PurelyImplements.kt")
                sourceRoots.from("$kotlin_stdlib_dir/jvm/runtime/kotlin/TypeAliases.kt")
                sourceRoots.from("$kotlin_stdlib_dir/jvm/runtime/kotlin/text/TypeAliases.kt")

                // for Kotlin 1.0 and 1.1 hack: Common platform becomes JVM
                if (version == "1.0" || version == "1.1") {
                    sourceRoots.from("$kotlin_root/core/builtins/native")
                    sourceRoots.from("$kotlin_root/core/builtins/src/")

                    sourceRoots.from("$kotlin_stdlib_dir/common/src")
                    sourceRoots.from("$kotlin_stdlib_dir/src")
                    sourceRoots.from("$kotlin_stdlib_dir/unsigned/src")
                }
            }
            register("kotlin-stdlib-jdk8") {
                jdkVersion.set(8)
                platform.set(Platform.jvm)
                classpath.setFrom(stdlibJvmClasspath)

                displayName.set("JRE8")
                dependsOn("kotlin-stdlib-java-common")
                if (version != "1.0" && version != "1.1") {
                    dependsOn("kotlin-stdlib-common")
                }
                sourceRoots.from("$kotlin_stdlib_dir/jdk8/src")
            }
            register("kotlin-stdlib-jdk7") {
                jdkVersion.set(8)
                platform.set(Platform.jvm)
                classpath.setFrom(stdlibJvmClasspath)


                displayName.set("JRE7")
                dependsOn("kotlin-stdlib-java-common")
                if (version != "1.0" && version != "1.1") {
                    dependsOn("kotlin-stdlib-common")
                }
                sourceRoots.from("$kotlin_stdlib_dir/jdk7/src")
            }
            if (version != "1.0") { // JS platform since Kotlin 1.1
                register("kotlin-stdlib-js") {
                    jdkVersion.set(8)
                    platform.set(Platform.js)
                    noJdkLink.set(true)
                    classpath.setFrom(stdlibJsClasspath)

                    displayName.set("JS")
                    if (version != "1.0" && version != "1.1") {
                        dependsOn("kotlin-stdlib-common")
                    }
                    //sourceRoots.from("$kotlin_root/core/builtins/native")
                    //sourceRoots.from("$kotlin_root/core/builtins/src")

                    sourceRoots.from("$kotlin_stdlib_dir/js/src")
                    sourceRoots.from("$kotlin_stdlib_dir/js-v1/src")

                    // for Kotlin 1.1 hack: Common platform becomes JVM
                    if (version == "1.1") {
                        sourceRoots.from("$kotlin_root/core/builtins/native")
                        sourceRoots.from("$kotlin_root/core/builtins/src/")

                        //sourceRoots.from("$kotlin_stdlib_dir/common/src") // is included  in /js-v1/src folder
                        sourceRoots.from("$kotlin_stdlib_dir/src")
                        sourceRoots.from("$kotlin_stdlib_dir/unsigned/src")
                    }
                    perPackageOption {
                        matchingRegex.set("org.w3c(\$|\\.).*")
                        reportUndocumented.set(false)
                    }
                    perPackageOption {
                        matchingRegex.set("org.khronos(\$|\\.).*")
                        reportUndocumented.set(false)
                    }
                }
            }
            if (version != "1.0" && version != "1.1" && version != "1.2") { // Native platform since Kotlin 1.3
                register("kotlin-stdlib-native") {
                    jdkVersion.set(8)
                    platform.set(Platform.native)
                    noJdkLink.set(true)
                    classpath.setFrom(stdlibNativeClasspath)

                    displayName.set("Native")
                    dependsOn("kotlin-stdlib-common")

                    sourceRoots.from("$kotlin_native_root/Interop/Runtime/src/main/kotlin")
                    sourceRoots.from("$kotlin_native_root/Interop/Runtime/src/native/kotlin")
                    sourceRoots.from("$kotlin_native_root/Interop/JsRuntime/src/main/kotlin")
                    sourceRoots.from("$kotlin_native_root/runtime/src/main/kotlin")
                    sourceRoots.from("$kotlin_stdlib_dir/native-wasm/src")
                    perPackageOption {
                        matchingRegex.set("kotlin.test(\$|\\.).*")
                        suppress.set(true)
                    }
                }
            }
            configureEach {
                documentedVisibilities.set([DokkaConfiguration.Visibility.PUBLIC, DokkaConfiguration.Visibility.PROTECTED])
                skipDeprecated.set(false)
                includes.from(stdlibIncludeMd.toString())
                noStdlibLink.set(true)
                languageVersion.set(kotlinLanguageVersion)
                samples.from(stdlibSamples.toString())
                suppressedPackages.forEach { packageName ->
                    perPackageOption {
                        matchingRegex.set("${packageName.replace(".", "\\.")}(\$|\\..*)")
                        suppress.set(true)
                    }
                }
                sourceLink {
                    localDirectory.set(file(localRoot))
                    remoteUrl.set(baseUrl)
                    remoteLineSuffix.set("#L")
                }
            }
        }
    }
}

TaskProvider<DokkaTask> createKotlinTestVersionedDocTask(String version, Boolean isLatest, TaskProvider<DokkaTask> stdlibDocTask) {
    return tasks.register("kotlin-test_" + version + (isLatest ? "_latest" : ""), DokkaTask) {
        dependsOn(prepare, stdlibDocTask)

        def kotlinTestIncludeMd = "$kotlin_root/libraries/kotlin.test/Module.md"

        def kotlinTestCommonClasspath = ["$kotlin_libs/kotlin-test-common".toString()]
        def kotlinTestJunitClasspath = ["$kotlin_libs/kotlin-test-junit".toString()]
        def kotlinTestJunit5Classpath = ["$kotlin_libs/kotlin-test-junit5".toString()]
        def kotlinTestTestngClasspath = ["$kotlin_libs/kotlin-test-testng".toString()]
        def kotlinTestJsClasspath = ["$kotlin_libs/kotlin-test-js".toString()]
        def kotlinTestJvmClasspath = ["$kotlin_libs/kotlin-test".toString()]

        def stdlibPackageList = new URL("file:///${stdlibDocTask.get().outputDirectory.get()}/stdlib/package-list".toString())
        def junit5PackageList = new URL("https://junit.org/junit5/docs/current/api/element-list".toString())
        def kotlinLanguageVersion = version

        moduleName.set("kotlin.test")

        if (isLatest) {
            outputDirectory.set(new File(outputDir, "/kotlin.test/latest"))
            pluginsMapConfiguration.set(["org.jetbrains.dokka.base.DokkaBase"             : """{ "mergeImplicitExpectActualDeclarations": "true", "templatesDir": "$templatesDir" }""",
                                         "org.jetbrains.dokka.versioning.VersioningPlugin": """{ "version": "$version", "olderVersionsDir": "${outputDir.toString().replace('\\', '/')}//kotlin.test/old" }"""])
        } else {
            outputDirectory.set(new File(outputDir, "/kotlin.test/old/" + version))
            pluginsMapConfiguration.set(["org.jetbrains.dokka.base.DokkaBase"                : """{ "mergeImplicitExpectActualDeclarations": "true", "templatesDir": "$templatesDir" }""",
                                         "org.jetbrains.dokka.kotlinlang.VersionFilterPlugin": """{ "targetVersion": "$version" }""",
                                         "org.jetbrains.dokka.versioning.VersioningPlugin"   : """{ "version": "$version" }"""])
        }

        dokkaSourceSets {
            "kotlin-test-common" {
                jdkVersion.set(8)
                platform.set(Platform.common)
                classpath.setFrom(kotlinTestCommonClasspath)
                noJdkLink.set(true)

                displayName.set("Common")
                sourceRoots.from("$kotlin_root/libraries/kotlin.test/common/src/main/kotlin")
                sourceRoots.from("$kotlin_root/libraries/kotlin.test/annotations-common/src/main/kotlin")
            }

            "kotlin-test-jvm" {
                jdkVersion.set(8)
                platform.set(Platform.jvm)
                classpath.setFrom(kotlinTestJvmClasspath)

                displayName.set("JVM")
                sourceRoots.from("$kotlin_root/libraries/kotlin.test/jvm/src/main/kotlin")
            }

            "kotlin-test-JUnit" {
                jdkVersion.set(8)
                platform.set(Platform.jvm)
                classpath.setFrom(kotlinTestJunitClasspath)

                displayName.set("JUnit")
                sourceRoots.from("$kotlin_root/libraries/kotlin.test/junit/src/main/kotlin")

                externalDocumentationLink {
                    url.set(new URL("http://junit.org/junit4/javadoc/latest/"))
                    packageListUrl.set(new URL("http://junit.org/junit4/javadoc/latest/package-list"))
                }
            }

            "kotlin-test-JUnit5" {
                jdkVersion.set(8)
                platform.set(Platform.jvm)
                classpath.setFrom(kotlinTestJunit5Classpath)

                displayName.set("JUnit5")
                sourceRoots.from("$kotlin_root/libraries/kotlin.test/junit5/src/main/kotlin")

                externalDocumentationLink {
                    url.set(new URL("https://junit.org/junit5/docs/current/api/"))
                    packageListUrl.set(junit5PackageList)
                }
            }

            "kotlin-test-TestNG" {
                jdkVersion.set(8)
                platform.set(Platform.jvm)
                classpath.setFrom(kotlinTestTestngClasspath)

                displayName.set("TestNG")
                sourceRoots.from("$kotlin_root/libraries/kotlin.test/testng/src/main/kotlin")

                // externalDocumentationLink {
                //     url.set(new URL("https://jitpack.io/com/github/cbeust/testng/master/javadoc/"))
                //     packageListUrl.set(new URL("https://jitpack.io/com/github/cbeust/testng/master/javadoc/package-list"))
                // }
            }
            if (version != "1.0") { // JS platform since Kotlin 1.1
                "kotlin-test-js" {
                    platform.set(Platform.js)
                    classpath.setFrom(kotlinTestJsClasspath)
                    noJdkLink.set(true)

                    displayName.set("JS")
                    sourceRoots.from("$kotlin_root/libraries/kotlin.test/js/src/main/kotlin")
                }
            }
            if (version != "1.0" && version != "1.1" && version != "1.2") { // Native platform since Kotlin 1.3
                "kotlin-test-native" {
                    platform.set(Platform.native)
                    classpath.setFrom(kotlinTestJsClasspath)
                    noJdkLink.set(true)

                    displayName.set("Native")
                    sourceRoots.from("$kotlin_native_root/runtime/src/main/kotlin/kotlin/test")
                }
            }
            configureEach {
                skipDeprecated.set(false)
                includes.from(kotlinTestIncludeMd.toString())
                languageVersion.set(kotlinLanguageVersion)
                noStdlibLink.set(true)
                sourceLink {
                    localDirectory.set(file(localRoot))
                    remoteUrl.set(baseUrl)
                    remoteLineSuffix.set("#L")
                }
                externalDocumentationLink {
                    url.set(new URL("https://kotlinlang.org/api/latest/jvm/stdlib/"))
                    packageListUrl.set(stdlibPackageList)
                }
            }
        }
    }
}

gradle.projectsEvaluated {
    def versions = ["1.0", "1.1", "1.2", "1.3", "1.4", "1.5", "1.6", "1.7", "1.8"]
    String latestVersion = versions.last()

    // builds this version/all versions as historical for the next versions builds
    tasks.register('buildAllVersions')
    // builds the latest version incorporating all previous historical versions docs
    tasks.register('buildLatestVersion')

    def latestStdlib = createStdLibVersionedDocTask(latestVersion, true)
    def latestTest = createKotlinTestVersionedDocTask(latestVersion, true, latestStdlib)

    buildLatestVersion.configure { dependsOn(latestStdlib, latestTest) }

    versions.forEach { version ->
        def versionStdlib = createStdLibVersionedDocTask(version, false)
        def versionTest = createKotlinTestVersionedDocTask(version, false, versionStdlib)
        if (version != latestVersion) {
            latestStdlib.configure { dependsOn versionStdlib }
            latestTest.configure { dependsOn versionTest }
        }
        buildAllVersions.configure { dependsOn(versionStdlib, versionTest) }
    }
}
