import plugins.KotlinBuildPublishingPlugin

plugins {
    id("java-gradle-plugin")
    id("gradle-plugin-common-configuration")
    id("com.gradle.plugin-publish")
    id("jps-compatible")
}

repositories {
    mavenLocal()
    mavenCentral()
}

pill {
    variant = 'FULL'
}

dependencies {
    api project(':kotlin-gradle-plugin-api')
    // Use this dependency instead when building apart from the other modules:
    // compile "org.jetbrains.kotlin:kotlin-gradle-plugin-api:$kotlin_version"

    api project(':kotlin-test:kotlin-test-junit')

    compileOnly kotlinStdlib()
    compileOnly project(':compiler')
    compileOnly project(':compiler:plugin-api')
    compileOnly project(':compiler:cli-common')
    compileOnly project(':compiler:cli')
    
    compileOnly gradleApi()

    compileOnly project(':dependencies:intellij-core')
}

// Relocate `com.intellij.*` and some other classes to match those in the `kotlin-compiler-embeddable`
// (for example, the actual package at runtime is `org.jetbrains.kotlin.com.intellij.*`):
ArtifactsKt.runtimeJar(project, EmbeddableKt.rewriteDefaultJarDepsToShadedCompiler(project, {}), {})
// In a standalone build, you can setup the relocation with the Shadow plugin.

// You should configure your own Gradle plugin publication!
extensions.configure(GradlePluginDevelopmentExtension) {
    it.setAutomatedPublishing(false)
}

gradlePlugin {
    plugins {
        create("gradle-subplugin-example") {
            id = "org.jetbrains.kotlin.gradle-subplugin-example"
            implementationClass = "example.ExampleSubplugin"
        }
    }
}

pluginBundle {
    plugins {
        named("gradle-subplugin-example") {
            id = "org.jetbrains.kotlin.gradle-subplugin-example"
            displayName = "Kotlin Gradle subplugin example"
        }
    }
}

PluginMarkersKt.publishPluginMarkers(project, true)

// Disable releasing for this plugin
// It is not intended to be released publicly
tasks.withType(PublishToMavenRepository)
        .configureEach {
            if (it.name.endsWith("PublicationTo${KotlinBuildPublishingPlugin.REPOSITORY_NAME}Repository")) {
                setEnabled(false)
            }
        }

tasks.named("publishPlugins") {
    enabled = false
}
