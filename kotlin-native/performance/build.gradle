import org.jetbrains.kotlin.MPPTools
import org.jetbrains.kotlin.UtilsKt

buildscript {
    ext.rootBuildDirectory = file('..')

    ext {
        def properties = new java.util.Properties()
        properties.load(new java.io.FileReader(project.file("../../gradle.properties")))
        properties.each { k, v->
            def key = k as String
            set(key, project.findProperty(key) ?: v)
        }
    }
    ext["withoutEmbedabble"] = true
    UtilsKt.kotlinInit(project, findProperty("cacheRedirectorEnabled")?.toString()?.toBoolean())
    ext["bootstrapKotlinRepo"] = BootstrapKt.getBootstrapKotlinRepo(project)
    ext["bootstrapKotlinVersion"] = BootstrapKt.getBootstrapKotlinVersion(project)

    apply from: "$rootBuildDirectory/gradle/loadRootProperties.gradle"

    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-build-gradle-plugin:${kotlinBuildProperties.buildGradlePluginVersion}")
    }
}

plugins {
    id("org.jetbrains.kotlin.multiplatform") apply false
}

repositories {
    mavenCentral()
}

def globalProperties = new java.util.Properties()
ext.kotlin_root = project.file("../..").absolutePath
project.logger.info("kotlin_root: $kotlin_root")
globalProperties.load(new java.io.FileReader(project.file("$kotlin_root/gradle.properties")))

subprojects { proj ->
    globalProperties.each { k, v->
        def key = k as String
        def value = project.findProperty(key) ?: v
        proj.logger.info("${proj.name}<<<[$key] = $value>>>")
        proj.ext.set(key, value)
    }
    UtilsKt.kotlinInit(proj, findProperty("cacheRedirectorEnabled")?.toString()?.toBoolean() ?: false)
    proj.ext["bootstrapKotlinRepo"] = BootstrapKt.getBootstrapKotlinRepo(proj)
    proj.ext["bootstrapKotlinVersion"] = BootstrapKt.getBootstrapKotlinVersion(proj)
    proj.ext["kotlin.native.home"] = UtilsKt.getKotlinNativeDist(project).toString()
    proj.ext["konan.home"] = proj.ext["kotlin.native.home"]
    proj.ext["kotlin.native.enabled"] = true
    proj.logger.info("${proj.name}<<<[kotlin.native.home] = ${proj.ext["kotlin.native.home"]}")

    proj.ext["buildNumber"]  = findProperty("build.number")?.toString() ?: defaultSnapshotVersion
    proj.ext["kotlinVersion"] = findProperty("deployVersion")?.toString()?.identity { deploySnapshotStr ->
                if (deploySnapshotStr != "default.snapshot") deploySnapshotStr else defaultSnapshotVersion
            } ?: proj.ext["buildNumber"]
    proj.logger.info("${proj.name}<<<[kotlinVersion] = ${proj.ext["kotlinVersion"]}>>>")

    proj.repositories {
        mavenCentral()
    }
}

tasks.register("buildAnalyzer") {
    dependsOn(":benchmarksAnalyzer:${getHostAsTargetName()}Binaries".toString())
}

task konanRun {
    subprojects.each {
        if (it.name != "benchmarksAnalyzer" && it.name != "benchmarksLauncher") {
            dependsOn "${it.path}:konanRun"
        }
    }
}

task clean {
    subprojects.each {
        dependsOn "${it.path}:clean"
    }
    doLast {
        delete(layout.buildDirectory)
    }
}

defaultTasks 'konanRun'

private static String getHostAsTargetName() {
    String target = System.getProperty("os.name")
    if (target == 'Linux') return 'linuxX64'
    if (target.startsWith('Windows')) return 'mingwX64'
    if (target.startsWith('Mac')) return 'macosArm64'
    return 'unknown'
}

def mergeReports(String fileName) {
    def reports = []
    subprojects.each {
        def reportFile = it.layout.buildDirectory.file(fileName).get().asFile
        if (reportFile.exists()) {
            reports.add(reportFile)
        }
    }
    def output = MPPTools.mergeReports(reports)
    mkdir layout.buildDirectory.get().asFile.absolutePath
    new File("${layout.buildDirectory.get().asFile.absolutePath}/${fileName}").write(output)
}

task mergeNativeReports {
    doLast {
        mergeReports(nativeJson)
    }
}

subprojects.each {
    if (it.name != "benchmarksAnalyzer" && it.name != "benchmarksLauncher") {
        it.afterEvaluate {
            it.konanJsonReport.finalizedBy mergeNativeReports
        }
    }
}

// CI calls this task to check that compilation is not broken in Aggregate
task compileKotlinNative {
    dependsOn getTasksByName("compileKotlin${getHostAsTargetName().capitalize()}", true)
}

task cinterop {
    dependsOn 'clean'
    dependsOn 'cinterop:konanRun'
}

task helloworld {
    dependsOn 'clean'
    dependsOn 'helloworld:konanRun'
}

task objcinterop {
    dependsOn 'clean'
    dependsOn 'objcinterop:konanRun'
}

task ring {
    dependsOn 'clean'
    dependsOn 'ring:konanRun'
}

task numerical {
    dependsOn 'clean'
    dependsOn 'numerical:konanRun'
}

task startup {
    dependsOn 'clean'
    dependsOn 'startup:konanRun'
}

task swiftinterop {
    dependsOn 'clean'
    dependsOn 'swiftinterop:konanRun'
}
