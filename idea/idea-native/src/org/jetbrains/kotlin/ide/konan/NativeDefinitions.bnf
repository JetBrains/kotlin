{
  parserClass="org.jetbrains.kotlin.ide.konan.NativeDefinitionsParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="NativeDefinitions"
  psiImplClassSuffix="Impl"
  psiPackage="org.jetbrains.kotlin.ide.konan.psi"
  psiImplPackage="org.jetbrains.kotlin.ide.konan.psi"

  elementTypeHolderClass="org.jetbrains.kotlin.ide.konan.psi.NativeDefinitionsTypes"
  elementTypeClass="org.jetbrains.kotlin.ide.konan.psi.NativeDefinitionsElementType"
  tokenTypeClass="org.jetbrains.kotlin.ide.konan.psi.NativeDefinitionsTokenType"

  tokens = [
    // known properties
    COMPILER_OPTS="compilerOpts"
    DEPENDS="depends"
    DISABLE_DESIGNATED_INITIALIZER_CHECKS="disableDesignatedInitializerChecks"
    ENTRY_POINT="entryPoint"
    EXCLUDE_DEPENDENT_MODULES="excludeDependentModules"
    EXCLUDED_FUNCTIONS="excludedFunctions"
    EXCLUDED_MACROS="excludedMacros"
    EXCLUDE_SYSTEM_LIBS="excludeSystemLibs"
    EXPORT_FORWARD_DECLARATIONS="exportForwardDeclarations"
    HEADER_FILTER="headerFilter"
    HEADERS="headers"
    LANGUAGE="language"
    LIBRARY_PATHS="libraryPaths"
    LINKER="linker"
    LINKER_OPTS="linkerOpts"
    MODULES="modules"
    NON_STRICT_ENUMS="nonStrictEnums"
    NO_STRING_CONVERSION="noStringConversion"
    PACKAGE="package"
    STATIC_LIBRARIES="staticLibraries"
    STRICT_ENUMS="strictEnums"

    // known platforms
    ANDROID="android"
    ANDROID_ARM32="android_arm32"
    ANDROID_ARM64="android_arm64"
    ARM32="arm32"
    ARM64="arm64"
    IOS="ios"
    IOS_ARM32="ios_arm32"
    IOS_ARM64="ios_arm64"
    IOS_X64="ios_x64"
    LINUX="linux"
    LINUX_ARM32_HFP="linux_arm32_hfp"
    LINUX_MIPS32="linux_mips32"
    LINUX_MIPSEL32="linux_mipsel32"
    LINUX_X64="linux_x64"
    MACOS_X64="macos_x64"
    MINGW="mingw"
    MINGW_X64="mingw_x64"
    MIPS32="mips32"
    MIPSEL32="mipsel32"
    OSX="osx"
    WASM="wasm"
    WASM32="wasm32"
    X64="x64"

    UNKNOWN_KEY="<unknown key>"
    UNKNOWN_PLATFORM="<unknown platform>"
  ]
}

root ::= definitionItem_* (DELIM code)?

private definitionItem_ ::= COMMENT | boolDefinition_ | stringDefinition_ | stringsDefinition_ | incorrectDefinition_

private boolDefinition_ ::= boolKey platform_? SEPARATOR boolValue
boolKey ::= DISABLE_DESIGNATED_INITIALIZER_CHECKS | EXCLUDE_DEPENDENT_MODULES | EXCLUDE_SYSTEM_LIBS
boolValue ::= VALUE

private stringDefinition_ ::= stringKey SEPARATOR stringValue
stringKey ::= extensibleStringKey_ platform_? | PACKAGE
private extensibleStringKey_ ::= LANGUAGE | LINKER
stringValue ::= VALUE

private stringsDefinition_ ::= stringsKey platform_? SEPARATOR stringsValue
stringsKey ::= COMPILER_OPTS | DEPENDS  | ENTRY_POINT | EXCLUDED_FUNCTIONS
                            | EXCLUDED_MACROS  | EXPORT_FORWARD_DECLARATIONS | HEADER_FILTER
                            | HEADERS | LIBRARY_PATHS | LINKER_OPTS | MODULES | NON_STRICT_ENUMS
                            | NO_STRING_CONVERSION | PACKAGE | STATIC_LIBRARIES | STRICT_ENUMS

stringsValue ::= VALUE

private incorrectDefinition_ ::= UNKNOWN_KEY? UNKNOWN_PLATFORM? SEPARATOR VALUE? | UNKNOWN_KEY UNKNOWN_PLATFORM?

private platform_ ::= ANDROID | ANDROID_ARM32 | ANDROID_ARM64 | ARM32 | ARM64 | IOS | IOS_ARM32
                        | IOS_ARM64 | IOS_X64 | LINUX | LINUX_ARM32_HFP | LINUX_MIPS32
                        | LINUX_MIPSEL32 | LINUX_X64 | MACOS_X64 | MINGW | MINGW_X64 | MIPS32
                        | MIPSEL32 | OSX | WASM | WASM32 | X64 | UNKNOWN_PLATFORM

code ::= CODE_CHARS