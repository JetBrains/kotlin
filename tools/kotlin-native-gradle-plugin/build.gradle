/*
 * Copyright 2010-2017 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.jetbrains.kotlin.konan.KonanVersion

/**
 * One may use bintrayUser/bintrayKey project properties or BINTRAY_USER/BINTRAY_KEY environment variables to upload
 * built plugin to bintray repository.
 */

buildscript {
    ext.rootBuildDirectory = file('../../')

    apply from: "$rootBuildDirectory/gradle/loadRootProperties.gradle"
    apply from: "$rootBuildDirectory/gradle/kotlinGradlePlugin.gradle"

    repositories {
        maven {
            url = 'https://cache-redirector.jetbrains.com/jcenter'
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-native-shared:$konanVersion"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}

plugins {
    id 'com.gradle.plugin-publish' version '0.9.10'
}

apply plugin: 'java-gradle-plugin'
apply plugin: 'kotlin'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'org.jetbrains.kotlin'
version = KonanVersion.Companion.CURRENT.toString()

repositories {
    mavenCentral()
    maven {
        url kotlinCompilerRepo
    }
}

configurations {
    bundleDependencies {
        transitive = false
    }

    implementation.extendsFrom shadow
    compileOnly.extendsFrom bundleDependencies
    testImplementation.extendsFrom bundleDependencies
}

dependencies {
    shadow "org.jetbrains.kotlin:kotlin-stdlib:$buildKotlinVersion"
    shadow "org.jetbrains.kotlin:kotlin-gradle-plugin:$buildKotlinVersion"

    bundleDependencies "org.jetbrains.kotlin:kotlin-native-shared:$konanVersion"

    testImplementation 'junit:junit:4.12'
    testImplementation "org.jetbrains.kotlin:kotlin-test:$buildKotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$buildKotlinVersion"
    testImplementation "org.tools4j:tools4j-spockito:1.6"
    testImplementation('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

jar {
    appendix = "no-shared"
}

shadowJar {
    configurations = [project.configurations.bundleDependencies]
    classifier = null
}

assemble.dependsOn shadowJar

pluginUnderTestMetadata {
    dependsOn shadowJar
    pluginClasspath = files(shadowJar.archivePath) + configurations.shadow
}

test {
    systemProperty("kotlin.version", buildKotlinVersion)
    if (project.hasProperty("konan.home")) {
        systemProperty("konan.home", project.property("konan.home"))
    } else {
        // The Koltin/Native compiler must be built before test execution.
        systemProperty("konan.home", distDir.absolutePath)
    }
    if (project.hasProperty("konan.jvmArgs")) {
        systemProperty("konan.jvmArgs", project.property("konan.jvmArgs"))
    }
    // Uncomment for debugging.
    //testLogging.showStandardStreams = true
    if (project.hasProperty("maxParallelForks")) {
        maxParallelForks=project.property("maxParallelForks")
    }
    if (project.hasProperty("filter")) {
        filter.includeTestsMatching project.property("filter")
    }
}

processResources {
    from(file("$rootBuildDirectory/utilities/env_blacklist"))
}

compileTestGroovy.dependsOn.remove('compileTestJava')
compileTestKotlin.dependsOn compileTestGroovy
compileTestKotlin.classpath += files(compileTestGroovy.destinationDir)

publishing {
    publications {
        gradlePlugin(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'kotlin-native-dependencies'
        name = 'kotlin-native-gradle-plugin'
        userOrg = 'jetbrains'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/JetBrains/kotlin-native'
        version {
            name = project.version
            desc = "Kotlin Native Gradle plugin $konanVersion"
        }
        publish  = project.findProperty("bintrayPublish").toString().toBoolean()
        override = project.findProperty("bintrayOverride").toString().toBoolean()
    }
    publications = ['gradlePlugin']
}

gradlePlugin {
    plugins {
        create('konan') {
            id = 'konan'
            implementationClass = 'org.jetbrains.kotlin.gradle.plugin.KonanPlugin'
        }
        create('kotlin-native') {
            id = 'kotlin-native'
            implementationClass = 'org.jetbrains.kotlin.gradle.plugin.experimental.plugins.KotlinNativePlugin'
        }
        create('kotlin-platform-native') {
            id = 'kotlin-platform-native'
            implementationClass = 'org.jetbrains.kotlin.gradle.plugin.experimental.plugins.KotlinPlatformNativePlugin'
        }

        create('org.jetbrains.kotlin.konan') {
            id = 'org.jetbrains.kotlin.konan'
            implementationClass = 'org.jetbrains.kotlin.gradle.plugin.KonanPlugin'
        }
        create('org.jetbrains.kotlin.native') {
            id = 'org.jetbrains.kotlin.native'
            implementationClass = 'org.jetbrains.kotlin.gradle.plugin.experimental.plugins.KotlinNativePlugin'
        }
        create('org.jetbrains.kotlin.platform.native') {
            id = 'org.jetbrains.kotlin.platform.native'
            implementationClass = 'org.jetbrains.kotlin.gradle.plugin.experimental.plugins.KotlinPlatformNativePlugin'
        }
    }
}

// Replace the default output jars with the fat-jar created by the shadowJar task.
configurations.archives.artifacts.clear()

artifacts {
    archives shadowJar
}

pluginBundle {
    website = 'https://kotlinlang.org/'
    vcsUrl = 'https://github.com/JetBrains/kotlin-native'
    description = 'Kotlin/Native plugins for Gradle'
    tags = ['kotlin']

    mavenCoordinates {
        groupId = "org.jetbrains.kotlin"
    }

    plugins {
        konan {
            id = 'org.jetbrains.kotlin.konan'
            description = displayName = 'Kotlin/Native plugin'
        }
        kotlinNative {
            id = 'org.jetbrains.kotlin.native'
            displayName = 'Experimental Kotlin/Native plugin'
            description = """\
                An experimental Kotlin/Native plugin integrated with Gradle support for native languages and
                providing a new DSL which is much closer to the DSL of Kotlin/JVM and Kotlin/JS plugins.
                
                The plugin is intended for projects containing only Kotlin/Native code (without common modules).
                For multiplatform development see https://plugins.gradle.org/plugin/org.jetbrains.kotlin.platform.native.
                """.stripIndent()
        }
        kotlinPlatformNative {
            id = 'org.jetbrains.kotlin.platform.native'
            displayName = 'Experimental Kotlin/Native plugin for multiplatform development'
            description = """\
                An experimental Kotlin/Native plugin integrated with Gradle support for native languages and
                providing a new DSL which is much closer to the DSL of Kotlin/JVM and Kotlin/JS plugins.
                
                The plugin is intended for native parts of multiplatform projects.
                """.stripIndent()
        }
    }
}
