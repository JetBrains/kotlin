apply plugin: 'kotlin-multiplatform'
apply plugin: 'maven-publish'

group = 'org.example'
version = '1.0'

repositories {
    mavenLocal()
    jcenter()
}

kotlin {
    sourceSets {
        allNative {
            dependencies {
                implementation 'com.example:sample-lib:1.0'
            }
        }
        nativeTest

        macos64Main { dependsOn sourceSets.allNative }
        linux64Main { dependsOn sourceSets.allNative }
        mingw64Main { dependsOn sourceSets.allNative }
        mingw86Main { dependsOn sourceSets.allNative }

        macos64Test { dependsOn sourceSets.nativeTest }
        linux64Test { dependsOn sourceSets.nativeTest }
        mingw64Test { dependsOn sourceSets.nativeTest }
        mingw86Test { dependsOn sourceSets.nativeTest }
    }

    targets {
        fromPreset(presets.macosX64, 'macos64')
        fromPreset(presets.linuxX64, 'linux64')
        fromPreset(presets.mingwX64, 'mingw64')
        // Test building a 32-bit Windows binary.
        fromPreset(presets.mingwX86, 'mingw86')

        configure([macos64, linux64, mingw64, mingw86]) {
            compilations.all {
                // Add interop in the test compilation too as a regression test for KT-30290.
                cinterops {
                    stdio {
                        packageName 'example.cinterop.published.stdio'
                        extraOpts '-nodefaultlibs'
                    }
                }
            }
        }
    }

}

// We don't create test tasks for Windows x86. But we still want to test
// generating interop for it, so we add this dependency.
build.dependsOn 'linkDebugTestMingw86'

publishing {
    repositories {
        maven {
            url = '../repo'
        }
    }
}
