buildscript {
    ext.rootBuildDirectory = file('../..')

    ext {
        def properties = new java.util.Properties()
        properties.load(new java.io.FileReader(project.file("$rootBuildDirectory/../gradle.properties")))
        properties.each { k, v ->
            def key = k as String
            def value = project.findProperty(key) ?: v
            project.logger.info("${project.name} $key: $value")
            set(key, value)
        }
    }
    ext["withoutEmbedabble"] = true
    ext["kotlinVersion"] = project.bootstrapKotlinVersion
    // Hack to override 'kotlin.native.home' value programmatically, but still allow
    // to redefine it via Gradle properties
    ext["kotlin.native.home"] = providers.gradleProperty("kotlin.native.home")
            .orElse(file("../../dist").absolutePath)
            .get()

    apply from: "$rootBuildDirectory/gradle/loadRootProperties.gradle"
}

plugins {
    id("org.jetbrains.kotlin.multiplatform")
}

repositories {
    mavenCentral()
}

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
            }
            kotlin.srcDir '../benchmarks/shared/src'
            kotlin.srcDir 'src/main/kotlin'
            kotlin.srcDir '../../endorsedLibraries/kotlinx.cli/src/main/kotlin'
        }
        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
            }
            kotlin.srcDir 'src/tests'
        }
        nativeMain {
            dependsOn commonMain
            kotlin.srcDir 'src/main/kotlin-native'
            kotlin.srcDir '../../endorsedLibraries/kotlinx.cli/src/main/kotlin-native'
        }
        linuxMain { dependsOn nativeMain }
        windowsMain { dependsOn nativeMain }
        macosArm64Main { dependsOn nativeMain }
    }

    targets {
        mingwX64('windows') {
        }

        linuxX64('linux') {
        }

        macosArm64('macosArm64') {
        }

        configure([findByName('windows'), findByName('linux'), findByName('macosArm64')].findAll { it != null }) {
            binaries {
                executable('benchmarksAnalyzer', [RELEASE]) {
                }
            }
        }
    }
}