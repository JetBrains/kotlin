import org.jetbrains.kotlin.MPPTools
import org.jetbrains.kotlin.UtilsKt
import org.jetbrains.kotlin.konan.target.DistributionKt
import org.jetbrains.kotlin.konan.target.PlatformManager

buildscript {
    ext.rootBuildDirectory = file('..')

    ext {
        def properties = new java.util.Properties()
        properties.load(new java.io.FileReader(project.file("../../gradle.properties")))
        properties.each { k, v->
            def key = k as String
            set(key, project.findProperty(key) ?: v)
        }
    }
    ext["withoutEmbedabble"] = true
    UtilsKt.kotlinInit(project, findProperty("cacheRedirectorEnabled")?.toString()?.toBoolean())
    ext["bootstrapKotlinRepo"] = BootstrapKt.getBootstrapKotlinRepo(project)
    ext["bootstrapKotlinVersion"] = BootstrapKt.getBootstrapKotlinVersion(project)

    apply from: "$rootBuildDirectory/gradle/loadRootProperties.gradle"

    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-build-gradle-plugin:${kotlinBuildProperties.buildGradlePluginVersion}")
    }
}

plugins {
    id("kotlin.native.build-tools-conventions")
    id("org.jetbrains.kotlin.multiplatform") apply false
}

repositories {
    mavenCentral()
}

def globalProperties = new java.util.Properties()
ext.kotlin_root = project.file("../..").absolutePath
project.logger.info("kotlin_root: $kotlin_root")
globalProperties.load(new java.io.FileReader(project.file("$kotlin_root/gradle.properties")))

def konanDataDir = project.hasProperty('konan.data.dir') ? project.property('konan.data.dir').toString() : null
def platformManager = new PlatformManager(DistributionKt.buildDistribution(UtilsKt.getKotlinNativeDist(project).path, konanDataDir), false)
def kotlinDist = null
if (hasProperty("kotlin_dist")) {
    kotlinDist = file(findProperty("kotlin_dist"))
    ext["notationMapping"] = [':kotlin-stdlib-common' : project.file("${kotlinDist}/kotlinc/lib/kotlin-stdlib.jar").absolutePath,
                              ':kotlin-test'          : project.file("${kotlinDist}/kotlinc/lib/kotlin-test.jar").absolutePath,
                              ':kotlin-stdlib-jdk8'   : project.file("${kotlinDist}/kotlinc/lib/kotlin-stdlib-jdk8.jar").absolutePath]
    ext.targetList = []
}

subprojects { proj ->
    globalProperties.each { k, v->
        def key = k as String
        def value = project.findProperty(key) ?: v
        proj.logger.info("${proj.name}<<<[$key] = $value>>>")
        proj.ext.set(key, value)
    }
    UtilsKt.kotlinInit(proj, findProperty("cacheRedirectorEnabled")?.toString()?.toBoolean() ?: false)
    proj.ext.platformManager = platformManager
    proj.ext["bootstrapKotlinRepo"] = BootstrapKt.getBootstrapKotlinRepo(proj)
    proj.ext["bootstrapKotlinVersion"] = BootstrapKt.getBootstrapKotlinVersion(proj)
    proj.ext["kotlin.native.home"] = UtilsKt.getKotlinNativeDist(project).toString()
    proj.ext["konan.home"] = proj.ext["kotlin.native.home"]
    proj.ext["kotlin.native.enabled"] = true
    proj.logger.info("${proj.name}<<<[kotlin.native.home] = ${proj.ext["kotlin.native.home"]}")
    if (project.hasProperty("kotlin_dist")) {
        proj.ext["kotlin_dist"] = kotlinDist.path
        proj.logger.info("${proj.name}<<<[kotlin_dist] = ${proj.ext["kotlin_dist"]}>>>")
    }

    proj.ext["buildNumber"]  = findProperty("build.number")?.toString() ?: defaultSnapshotVersion
    proj.ext["kotlinVersion"] = findProperty("deployVersion")?.toString()?.identity { deploySnapshotStr ->
                if (deploySnapshotStr != "default.snapshot") deploySnapshotStr else defaultSnapshotVersion
            } ?: proj.ext["buildNumber"]
    proj.logger.info("${proj.name}<<<[kotlinVersion] = ${proj.ext["kotlinVersion"]}>>>")

    proj.repositories {
        mavenCentral()
    }
}

tasks.register("buildAnalyzer") {
    dependsOn(gradle.includedBuild("benchmarksAnalyzer").task(":${getAnalyzerTargetName()}Binaries".toString()))
}

task konanRun {
    subprojects.each {
        dependsOn "${it.path}:konanRun"
    }
}

task clean {
    subprojects.each {
        if (it.name != "endorsedLibraries") {
            dependsOn "${it.path}:clean"
        }
    }
    doLast {
        delete(layout.buildDirectory)
    }
}

defaultTasks 'konanRun'

private static String getAnalyzerTargetName() {
    // Copy-pasted from tools/benchmarksAnalyzer/build.gradle.
    String target = System.getProperty("os.name")
    if (target == 'Linux') return 'linux'
    if (target.startsWith('Windows')) return 'windows'
    if (target.startsWith('Mac'))
        if (System.getProperty("os.arch") == "aarch64")
            return 'macosArm64'
        else
            return 'macosX64'
    return 'unknown'
}

private String findAnalyzerBinary() {
    String result = "${projectDir}/../${analyzerToolDirectory}/${getAnalyzerTargetName()}/" +
            "${analyzerTool}ReleaseExecutable/${analyzerTool}${MPPTools.getNativeProgramExtension()}"
}

def mergeReports(String fileName) {
    def reports = []
    subprojects.each {
        def reportFile = it.layout.buildDirectory.file(fileName).get().asFile
        if (reportFile.exists()) {
            reports.add(reportFile)
        }
    }
    def output = MPPTools.mergeReports(reports)
    mkdir layout.buildDirectory.get().asFile.absolutePath
    new File("${layout.buildDirectory.get().asFile.absolutePath}/${fileName}").write(output)
}

task mergeNativeReports {
    doLast {
        mergeReports(nativeJson)
    }
}

subprojects.each {
    if (it.name != "endorsedLibraries" && it.name != "kotlinx.cli") {
        it.afterEvaluate {
            it.konanJsonReport.finalizedBy mergeNativeReports
        }
    }
}

task teamCityStat(type:Exec) {
    def analyzer = findAnalyzerBinary()
    if (getAnalyzerTargetName() == 'windows') {
        def mingwPath = new File(System.getenv("MINGW64_DIR") ?: "C:/msys64/mingw64")
        def pathEnv = getEnvironment()["PATH"]
        environment("PATH", "${mingwPath.toString()}/bin" + (pathEnv ? ";$pathEnv" : ""))
    }
    commandLine analyzer, "-r", "teamcity",
        "--artifactory-url", "https://repo.labs.intellij.net/kotlin-native-benchmarks",
        "--teamcity-url", "http://buildserver.labs.intellij.net",
        "--server-url", findProperty("kotlin.native.performance.server.url")?.toString() ?: "http://localhost:3000",
        "${layout.buildDirectory.file(nativeJson).get().asFile}"
}

task cinterop {
    dependsOn 'clean'
    dependsOn 'cinterop:konanRun'
}

task framework {
    dependsOn 'clean'
    dependsOn 'framework:konanRun'
}

task helloworld {
    dependsOn 'clean'
    dependsOn 'helloworld:konanRun'
}

task objcinterop {
    dependsOn 'clean'
    dependsOn 'objcinterop:konanRun'
}

task ring {
    dependsOn 'clean'
    dependsOn 'ring:konanRun'
}

task numerical {
    dependsOn 'clean'
    dependsOn 'numerical:konanRun'
}

task startup {
    dependsOn 'clean'
    dependsOn 'startup:konanRun'
}

task swiftinterop {
    dependsOn 'clean'
    dependsOn 'swiftinterop:konanRun'
}
