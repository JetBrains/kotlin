import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

android {
    compileSdkVersion 34
    namespace 'org.jetbrains.kotlin.gradle.test.androidalfa'

    sourceSets {
        main.java.srcDirs += 'src/main/java2'
        main.kotlin.srcDirs += 'root/kotlin'
        test.kotlin.srcDirs += 'src/test/kotlin'
    }

    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 34
    }

    // Needed for older AGP. It can be removed when the lowest supported AGP version is 4.2.0 or newer.
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    flavorDimensions "myFlavor"
    productFlavors {
        flavor1 {
            applicationId "com.example.flavor1"
            versionCode 20
            dimension "myFlavor"
        }
        flavor2 {
            applicationId "com.example.flavor2"
            minSdkVersion 27
            dimension "myFlavor"
        }
    }

    if (GradleVersion.current() < GradleVersion.version("8.5")) {
        publishNonDefault true
    }
    lintOptions.abortOnError = false
}

dependencies {
    implementation project(":Lib")
    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

kotlin.compilerOptions.jvmTarget = JvmTarget.JVM_1_8
