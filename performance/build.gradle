buildscript {
    repositories {
        mavenCentral()
        maven {
            url kotlinCompilerRepo
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:$konanVersion"
    }
}

repositories {
    maven {
        url kotlinCompilerRepo
    }
}

//TODO: property
def jvmWarmup = 10000
def nativeWarmup = 10

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'konan'

project.setProperty("konan.home", distDir)
checkKonanCompiler.dependsOn ':dist'

konanArtifacts {
    program('Ring') {
        srcDir 'src/main/kotlin'
        srcDir 'src/main/kotlin-native'
        enableOptimizations true
    }
}

sourceSets {
    main.kotlin.srcDir 'src/main/kotlin'
    main.kotlin.srcDir 'src/main/kotlin-jvm'
}

compileKotlin {
    kotlinOptions.suppressWarnings = true
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
}

task jvmRun(type: JavaExec) {
    dependsOn 'build'
    def output = new ByteArrayOutputStream()
    classpath sourceSets.main.runtimeClasspath
    main = "MainKt"
    args "$jvmWarmup"
    standardOutput = output
    doLast {
        dumpReport('jvmReport', output)
    }
}

private void dumpReport(String name, ByteArrayOutputStream output) {
    new File("${buildDir.absolutePath}/${name}.txt").withOutputStream {
        it.write(output.toByteArray())
    }
}

task konanRun(type: Exec) {
    dependsOn 'build'
    def output = new ByteArrayOutputStream()
    commandLine konanArtifacts.Ring.getByTarget('host').artifact.absolutePath, "$nativeWarmup"
    standardOutput = output
    doLast {
        dumpReport('konanReport', output)
    }
}

startScripts{
    setEnabled(false)
}

task bench(type:DefaultTask) {
    dependsOn jvmRun
    dependsOn konanRun

    doLast {
        def jvmReport = new Report(project.file("build/jvmReport.txt"))
        def konanReport = new Report(project.file("build/konanReport.txt"))
        def average = "none"
        def absoluteAverage = "none"
        jvmReport.report.each { k, v ->
            def konanValue = konanReport.report[k]
            def ratio = String.format('%.2f', konanValue/v)
            def formattedKonanValue = String.format('%.2f', konanValue/1000)
            if (k == 'RingAverage') {
                average = ratio
                absoluteAverage = formattedKonanValue
            }
            println("$k : absolute = $formattedKonanValue us, ratio = $ratio")
            if (System.getenv("TEAMCITY_BUILD_PROPERTIES_FILE") != null)
                println("##teamcity[buildStatisticValue key='$k' value='$ratio']")
        }

        println()
        println("Average Ring score: absolute = $absoluteAverage us, ratio = $average")
    }
}


class Report {
    def Map<String, Double> report = new TreeMap()

    Report(File path) {
        path.readLines().drop(3).findAll { it.split(':').length == 2 }.each {
            def p = it.split(':')
            report.put(p[0].trim(), Double.parseDouble(p[1].trim()))
        }
    }
}
