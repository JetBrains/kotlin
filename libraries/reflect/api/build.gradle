description = ''

apply plugin: 'kotlin'

JvmToolchain.configureJvmToolchain(project, JdkMajorVersion.JDK_1_6)
LibrariesCommon.disableDeprecatedJvmTargetWarning(project)

def includeJava9 = BuildPropertiesExtKt.getIncludeJava9(project.kotlinBuildProperties)

sourceSets {
    main {
        java {
            srcDir "${rootDir}/core/reflection.jvm/src"
        }
        resources {
            srcDir("${rootDir}/core/reflection.jvm/resources")
        }
    }

    if (includeJava9) {
        java9
    }
}

dependencies {
    compile kotlinStdlib()
    compileOnly project(':core:descriptors')
    compileOnly project(':core:descriptors.jvm')
    compileOnly project(':core:deserialization')
    compileOnly project(':core:descriptors.runtime')
    compileOnly project(':core:util.runtime')
    compileOnly "org.jetbrains:annotations:13.0"
}

if (includeJava9) {
    LibrariesCommon.configureJava9Compilation(
            project,
            'kotlin.reflect',
            [sourceSets.main.output, configurations.compileOnly.filter {
                !it.name.contains("kotlin-stdlib")
            }]
    )
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = [
                "-Xallow-kotlin-package",
                "-Xno-optimized-callable-references",
                "-Xno-kotlin-nothing-value-exception",
                "-Xsuppress-deprecated-jvm-target-warning",
                "-Xopt-in=kotlin.RequiresOptIn",
        ]
        moduleName = "kotlin-reflection"
    }
}

configureJvmIrBackend(project)

jar {
    manifestAttributes(manifest, project, "internal")
}

task java9Jar(type: Jar) {
    classifier = "java9"
    if (includeJava9) {
        from sourceSets.java9.output
    }
}

artifacts {
    archives java9Jar
    runtime java9Jar
}
