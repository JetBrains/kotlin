import org.gradle.util.VersionNumber

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(8))
}

android {
    compileSdkVersion 22
    namespace 'org.jetbrains.kotlin.gradle.test.androidalfa'

    sourceSets {
        main.java.srcDirs += 'src/main/java2'
        main.kotlin.srcDirs += 'root/kotlin'
        test.kotlin.srcDirs += 'src/test/kotlin'
    }

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 22
    }

    // Needed for older AGP. It can be removed when the lowest supported AGP version is 4.2.0 or newer.
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    flavorDimensions "myFlavor"
    productFlavors {
        flavor1 {
            applicationId "com.example.flavor1"
            versionCode 20
            dimension "myFlavor"
        }
        flavor2 {
            applicationId "com.example.flavor2"
            minSdkVersion 14
            dimension "myFlavor"
        }
    }

    publishNonDefault true
    lintOptions.abortOnError = false
}

if (VersionNumber.parse(android_tools_version) < VersionNumber.parse("3.0.0-alpha1")) {
    configurations {
        flavor1DebugCompile
        flavor1ReleaseCompile
        flavor2DebugCompile
        flavor2ReleaseCompile
    }

    dependencies {
        flavor1DebugCompile project(path: ':Lib', configuration: 'flavor1Debug')
        flavor1ReleaseCompile project(path: ':Lib', configuration: 'flavor1Release')
        flavor2DebugCompile project(path: ':Lib', configuration: 'flavor2Debug')
        flavor2ReleaseCompile project(path: ':Lib', configuration: 'flavor2Release')
    }
}
else {
    dependencies {
        implementation project(":Lib")
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation'junit:junit:4.13.2'
}
